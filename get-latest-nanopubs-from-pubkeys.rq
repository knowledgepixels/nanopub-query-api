#+ summary: Get latest nanopublications from a list of pubkeys
#+ method: GET
#+ endpoint: https://query.np.trustyuri.net/repo/empty

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?label ?pubkey ?date where {
  #================================================================================#
  # MULTI-VALUE PLACEHOLDER
  { select ?pubkeyhash {                                                           # <- ELEMENT VAR ?pubkeyhash
    { select * { optional { ?a ?b ?c } } limit 1 }  # for Virtuoso
    bind(?_pubkeyhashes as ?vs)                                                    # <- LIST VAR ?_pubkeyhashes
    optional { <x:> <x:> ?_pubkeyhashes }                                          # <- LIST VAR ?_pubkeyhashes
    values ?n { 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24
      25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49
      50 51 52 53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68 69 70 71 72 73 74
      75 76 77 78 79 80 81 82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 99}
    filter(if(?n=0, true, regex(?values, concat("^([^ ]* ){", str(?n) ,"}"))))
    bind(if(?n=0, ?vs, replace(?vs, concat("^([^ ]* ){",str(?n),"}"), "")) as ?t)
    bind(replace(?t, " .*", "") as ?pubkeyhash)                                    # <- ELEMENT VAR ?pubkeyhash
  } }
  #================================================================================#
  bind(uri(concat("https://query.np.trustyuri.net/repo/pubkey/", ?pubkeyhash)) as ?service)
  
  service ?service {
    graph npa:graph {
      ?np rdfs:label ?label ;
          npa:hasValidSignatureForPublicKey ?pubkey ;
          dct:created ?date .
      filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
      filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
      filter not exists { ?np npx:hasNanopubType npx:retracts . }
    }
  }
}
order by desc(?date)
limit 100
