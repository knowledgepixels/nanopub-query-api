#+ summary: "Subject References"
#+ description: "Returns all referenes (in the assertion graph) of the given subject IRI."
#+ endpoint: https://query.np.trustyuri.net/repo/full

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select ?pred ?obj ?date ?creator where {
  graph npa:graph {
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    ?np dct:created ?date .
    ?np dct:creator ?creator .
    ?np np:hasAssertion ?assertion .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
  graph ?assertion {
    ?_subj_iri ?pred ?obj .
  }
}
order by desc(?date)
