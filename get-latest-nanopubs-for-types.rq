#+ endpoint: https://query.knowledgepixels.com/repo/meta

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?np ?label ?userid ?username ?pubkey ?date where {
  {select ?np ?label ?pubkey ?date { graph npa:graph {
    ?np npa:hasValidSignatureForPublicKey ?__pubkey  .
    bind(?__pubkey as ?pubkey)
    ?np dct:created ?date .
    optional { ?np rdfs:label ?label . }
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }

    #=================== MULTI-VALUE PLACEHOLDER ====================#------ config: ------
    { select ?typev {                                                # <- VAR typev
      bind(?_types as ?v)                                            # <- VAR _types
      optional {<x:> <x:> ?_types} # (for grlc to pick it up)        # <- VAR _types
      {select*{optional{?a ?b ?c}}limit 1} # (for some Virtuoso v.)  #
      values ?x{0 1 2 3 4 5 6 7 8 9} values ?y{0 1 2 3 4 5 6 7 8 9}  #
      bind((10*?x)+?y as ?n) # (works for a maximum of 100 values)   #
      bind(" " as ?sep) # (separator as regex-compatible character)  # <- CONST sep
      bind(concat("^([^",?sep,"]*",?sep,"){",str(?n),"}") as ?p)     #
      bind(concat(?sep,".*") as ?p0)                                 #
      filter(if(?n=0,true,regex(?v,?p))) # (=0 check for Virtuoso)   #
      bind(replace(if(?n=0,?v,replace(?v,?p,"")),?p0,"") as ?typev)  # <- VAR type 
    } }                                                              #
    #================================================================#---------------------
    bind(uri(?typev) as ?type)

    ?np npx:hasNanopubType ?type .
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
  } } order by desc(?date) limit 50 }
  optional { service <https://query.knowledgepixels.com/repo/type/d3V8q_YYTFHCC4sP5dxeE2W39ihEgzUYStVDGaCv_fw> {  # (type: http://purl.org/nanopub/x/declaredBy)
    graph npa:graph {
      ?i_np npa:hasValidSignatureForPublicKey ?pubkey ;
        np:hasAssertion ?i_assertion .
      filter not exists { ?i_npx npx:invalidates ?i_np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    }
    graph ?i_assertion {
      ?__userid_iri foaf:name ?username .
    }
  } }
  graph npa:graph {
    ?np dct:creator ?__userid_iri .
  }
  bind(?__userid_iri as ?userid)
}
    
