#+ endpoint: https://query.knowledgepixels.com/repo/meta

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?np ?label ?userid ?username ?pubkey ?date where {
  { select * { graph npa:graph {
    ?np dct:created ?date .
    optional { ?np rdfs:label ?label . }

    #=================== MULTI-VALUE PLACEHOLDER ====================#------ config: ------
    optional { select ?type {                                                # <- VAR type
      filter(?_types != "")
      bind(?_types as ?v)                                            # <- VAR _types
      optional {<x:> <x:> ?_types} # (for grlc to pick it up)        # <- VAR _types
      {select*{optional{?a ?b ?c}}limit 1} # (for some Virtuoso v.)  #
      values ?x{0 1 2 3 4 5 6 7 8 9} values ?y{0 1 2 3 4 5 6 7 8 9}  #
      bind((10*?x)+?y as ?n) # (works for a maximum of 100 values)   #
      bind(" " as ?sep) # (separator as regex-compatible character)  # <- CONST sep
      bind(concat("^([^",?sep,"]*",?sep,"){",str(?n),"}") as ?p)     #
      bind(concat(?sep,".*") as ?p0)                                 #
      filter(if(?n=0,true,regex(?v,?p))) # (=0 check for Virtuoso)   #
      bind(uri(replace(if(?n=0,?v,replace(?v,?p,"")),?p0,"")) as ?type)  # <- VAR type 
    } }                                                              #
    #================================================================#---------------------
    filter(?_types = "" || bound(?type))

    ?np npx:hasNanopubType ?type .
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }

    ?np npa:hasValidSignatureForPublicKey ?__pubkey  .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?__pubkey . }
    bind(?__pubkey as ?pubkey)

    #=================== MULTI-VALUE PLACEHOLDER ====================#------ config: ------
    optional { select ?userid {                                                # <- VAR type
      filter(?_users != "")
      bind(?_users as ?v)                                            # <- VAR _types
      {select*{optional{?a ?b ?c}}limit 1} # (for some Virtuoso v.)  #
      values ?x{0 1 2 3 4 5 6 7 8 9} values ?y{0 1 2 3 4 5 6 7 8 9}  #
      bind((10*?x)+?y as ?n) # (works for a maximum of 100 values)   #
      bind(" " as ?sep) # (separator as regex-compatible character)  # <- CONST sep
      bind(concat("^([^",?sep,"]*",?sep,"){",str(?n),"}") as ?p)     #
      bind(concat(?sep,".*") as ?p0)                                 #
      filter(if(?n=0,true,regex(?v,?p))) # (=0 check for Virtuoso)   #
      bind(uri(replace(if(?n=0,?v,replace(?v,?p,"")),?p0,"")) as ?userid)  # <- VAR type 
    } }                                                              #
    #================================================================#---------------------
    filter(?_users = "" || bound(?userid))

    ?np dct:creator ?userid .
  } } order by desc(?date) limit 50 }
  optional { service <https://query.knowledgepixels.com/repo/type/d3V8q_YYTFHCC4sP5dxeE2W39ihEgzUYStVDGaCv_fw> {  # (type: http://purl.org/nanopub/x/declaredBy)
    graph npa:graph {
      ?i_np npa:hasValidSignatureForPublicKey ?pubkey ;
        np:hasAssertion ?i_assertion .
      filter not exists { ?i_npx npx:invalidates ?i_np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    }
    graph ?i_assertion {
      ?userid foaf:name ?username .
    }
  } }
}
